plugins {
  id 'net.researchgate.release' version '2.8.1'
  id 'com.github.breadmoirai.github-release' version '2.2.12'
  id 'java'
}

subprojects {
  apply plugin: 'groovy'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  ext['groovy.version'] = '3.0.9'

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      name = 'apized'
      url = 'https://maven.pkg.github.com/apized/apized'
    }
  }

  java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
    withSourcesJar()
  }

  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/apized/apized")
        credentials {
          username = System.getenv("GITHUB_ACTOR")
          password = System.getenv("GITHUB_TOKEN")
        }

      }
    }
    publications {
      maven(MavenPublication) {
        from components.java
      }
    }
  }
}

release {
  preTagCommitMessage = '[skip ci] [Gradle Release Plugin] - pre tag commit: '
  git {
    requireBranch = /(main|release-.*)/
  }
}

githubRelease {
  owner = 'apized'
  token = "${System.getenv("GITHUB_TOKEN")}"
  targetCommitish = "main"
  tagName = "${project.version.replaceAll('-SNAPSHOT', '')}"
  releaseName = "${project.version.replaceAll('-SNAPSHOT', '')}"
  try {
    body = changelog().call()
  } catch (Exception ignored) {
    //Do nothing
  }
}

commitNewVersion.dependsOn tasks.githubRelease
tasks.githubRelease.mustRunAfter createReleaseTag
