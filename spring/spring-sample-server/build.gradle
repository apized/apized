import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
  id 'groovy'
  id 'org.apized.spring' version "$version"
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
}

java {
  sourceCompatibility = JavaVersion.toVersion("21")
  targetCompatibility = JavaVersion.toVersion("21")
}

springBoot {
  mainClass = 'org.apized.spring.server.Application'
}

tasks.withType(Test).configureEach {
  environment "SPRING_MAIN_CLASS", springBoot.mainClass.get()
}

tasks.withType(Dockerfile) {
  instructions.set(
    new ArrayList<Dockerfile.Instruction>(instructions.get()).collectMany {
      if (it.keyword == Dockerfile.FromInstruction.KEYWORD) {
        [
          new Dockerfile.FromInstruction(new Dockerfile.From('alpine:3.20')),
          new Dockerfile.RunCommandInstruction('apk --no-cache add openjdk21')
        ]
      } else {
        [ it ]
      }
    }.flatten() as List<Dockerfile.Instruction>
  )
}
